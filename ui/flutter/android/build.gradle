buildscript {
    ext.kotlin_version = '1.9.22'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

subprojects { subproject ->
    subproject.plugins.withId('com.android.library') {
        subproject.android {
            if (namespace == null || namespace.isEmpty()) {
                def manifest = file("${subproject.projectDir}/src/main/AndroidManifest.xml")
                if (manifest.exists()) {
                    def manifestContent = manifest.getText()
                    def packagePattern = /package\s*=\s*["']([^"']+)["']/
                    def matcher = (manifestContent =~ packagePattern)
                    if (matcher.find()) {
                        namespace = matcher.group(1)
                    }
                }
            }
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}
